# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gstreamer0.10-ugly
pkgname=('gstreamer0.10-ugly' 'gstreamer0.10-ugly-plugins')
pkgver=0.10.19
pkgrel=13
arch=('i686' 'x86_64')
license=('LGPL')
#makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.34' 'libdvdread' 'lame' 'libmpeg2' 'a52dec' 'libmad' 'libsidplay' 'libcdio' 'libx264' 'x264' 'opencore-amr' 'git')
makedepends=('pkgconfig' 'git')
url="http://gstreamer.freedesktop.org/"
source=("git://anongit.freedesktop.org/gstreamer-sdk/gst-plugins-ugly#commit=d637756a8e569753e9869c2c0728288f5dbc5089"
        opencore-amr.patch
        cdio-cd-text-api.patch)
md5sums=('SKIP'
         'd8feb6c99bfaff8be6b2c48ea4e98e47'
         '6c1c665f864387f3a77d32231fedeaab')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gstreamer_0.10_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gstreamer_0.10
fi

prepare() {
  cd gst-plugins-ugly
  patch -Np0 -i ../opencore-amr.patch
  patch -Np1 -i ../cdio-cd-text-api.patch
  sed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.ac

if [ "$CARCH" = "x86_64" ]; then
  sed -i 's|libgstx264_la_LDFLAGS = |& -L/usr/local/lib64 |g' ext/x264/Makefile.am
fi

}

build() {
  cd gst-plugins-ugly
  NOCONFIGURE=1 ./autogen.sh
  export PKG_CONFIG_PATH=/usr/local/gstreamer/${dstdir}/lib/pkgconfig:${PKG_CONFIG_PATH}
  sed -i 's|-ldvdread|-ldvdread -ldl|g' configure

if [ "$CARCH" = "x86_64" ]; then
  export LDFLAGS="-L/usr/local/lib64"
fi

  ./configure --prefix=/usr/local/gstreamer/${dstdir} --sysconfdir=/usr/local/gstreamer/${dstdir}/etc --localstatedir=/var \
    --disable-static --enable-experimental --disable-gtk-doc \
    --with-package-name="GStreamer Ugly Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" --enable-shared
  make
  sed -e 's/gst-libs gst ext/gst-libs gst/' -i Makefile
}

check() {
  cd gst-plugins-ugly
  #make check
}

package_gstreamer0.10-ugly() {
  pkgdesc="GStreamer Multimedia Framework Ugly plugin libraries"
  depends=('gstreamer0.10-base>=0.10.34')
  options=('staticlibs')
  
  cd gst-plugins-ugly
  export PKG_CONFIG_PATH=/usr/local/gstreamer/${dstdir}/lib/pkgconfig:${PKG_CONFIG_PATH}
  make DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-ugly-plugins() {
  pkgdesc="GStreamer Multimedia Framework Ugly Plugins (gst-plugins-ugly)"
  depends=("gstreamer0.10-ugly=${pkgver}" 'libdvdread' 'lame' 'libmpeg2' 'a52dec' 'libmad' 'libsidplay' 'libcdio' 'libx264' 'opencore-amr')
  groups=('gstreamer0.10-plugins')
  replaces=('gstreamer0.10-dvdread' 'gstreamer0.10-mpeg2dec' 'gstreamer0.10-mad' 'gstreamer0.10-lame' 'gstreamer0.10-sidplay' 'gstreamer0.10-a52dec')
  conflicts=('gstreamer0.10-dvdread' 'gstreamer0.10-mpeg2dec' 'gstreamer0.10-mad' 'gstreamer0.10-lame' 'gstreamer0.10-sidplay' 'gstreamer0.10-a52dec')
  options=('staticlibs')

  cd gst-plugins-ugly
  export PKG_CONFIG_PATH=/usr/local/gstreamer/${dstdir}/lib/pkgconfig:${PKG_CONFIG_PATH}
  make -C ext DESTDIR="${pkgdir}" install
}
