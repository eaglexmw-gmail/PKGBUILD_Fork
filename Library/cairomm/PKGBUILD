# $Id$
# Maintainer: Kevin <kevin@archlinux.org>
# Contributor: criminy <criminy@gmail.com>

pkgname=cairomm
pkgver=1.10.0
pkgrel=2
pkgdesc="C++ bindings to Cairo vector graphics library"
arch=('i686' 'x86_64')
url="http://www.cairographics.org"
license=('LGPL' 'MPL')
#depends=('cairo' 'libsigc++')
depends=('cairo')
makedepends=('pkgconfig')
source=(http://www.cairographics.org/releases/${pkgname}-${pkgver}.tar.gz)
options=('!libtool' '!emptydirs' 'staticlibs')
sha1sums=('f08bf8a331067f0d1e876523f07238fba6b26b99')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr  --enable-static 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
