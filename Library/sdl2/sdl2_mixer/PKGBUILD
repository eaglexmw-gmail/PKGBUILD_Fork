# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=sdl2_mixer
pkgver=2.0.0
pkgrel=3
pkgdesc="A simple multi-channel audio mixer (Version 2)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org/projects/SDL_mixer"
license=('MIT')
#depends=('sdl2' 'libvorbis' 'libmodplug' 'smpeg2' 'flac')
#makedepends=('fluidsynth')
optdepends=('fluidsynth: MIDI software synth, replaces built-in timidity')
source=("$url/release/SDL2_mixer-${pkgver}.tar.gz")
md5sums=('65f6d80df073a1fb3bb537fbda031b50')
options=('staticlibs')

prepare() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"

  sed -i "s|/etc/timidity|/etc/timidity++|g" timidity/config.h
  sed -i "s|/etc/timidity++.cfg|/etc/timidity++/timidity.cfg|g" timidity/config.h
}

build() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"

  export CFLAGS+=" ${CFLAGS} -I/usr/local/include/libmodplug"
  sed -i 's|-lfluidsynth |& -lm -lglib-2.0 -ldbus-1 -ljack -lpthread -lasound -lreadline -lsndfile |g' configure
  ./configure --disable-shared --prefix=/usr --disable-music-flac-shared
  make
}

package() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
