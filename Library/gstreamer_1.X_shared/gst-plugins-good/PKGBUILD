# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-good
pkgver=1.6.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
#depends=('libpulse' 'jack' 'libsoup' 'gst-plugins-base-libs' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout' 'libvpx' 'gdk-pixbuf2' 'libcaca' 'libavc1394'    #'libiec61883' 'libxdamage' 'v4l-utils' 'cairo')
depends=('gdk-pixbuf2' 'cairo')
#makedepends=('gstreamer' 'speex' 'flac' 'libraw1394' 'python' 'gtk-doc')
#makedepends=('gtk-doc')
options=(!emptydirs staticlibs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('24b19db70b2a83461ebddfe20033db432dadfdb5d4b54ffb1dfa0d830134a177')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gstreamer_1.x_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gstreamer_1.x
fi

build() {
  cd $pkgname-$pkgver

  sed -i 's|-lrom1394 |-lrom1394 -lm |g' ./configure

  export PATH=$PATH:/usr/local/python2/bin/
  export PYTHON=python2_spec
  export PKG_CONFIG_PATH=/usr/local/gstreamer/${dstdir}/lib/pkgconfig:${PKG_CONFIG_PATH}

  ./configure --prefix=/usr/local/gstreamer/${dstdir} --sysconfdir=/usr/local/gstreamer/${dstdir}/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Good Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --disable-static --enable-shared --disable-static-plugins --disable-gtk-doc --disable-freetypetest

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  sed -i 's|tests			\\| \\|g' ./Makefile

  make
}

# Disabled for now: souphttpsrc times out, and takes a damn long time to do so
#check() {
#  cd $pkgname-$pkgver
#  make check
#}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
