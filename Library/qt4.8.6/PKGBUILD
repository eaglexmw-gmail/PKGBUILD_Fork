# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qt4
pkgver=4.8.6
pkgrel=4
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework'
depends=('libtiff' 'libpng' 'sqlite' 'ca-certificates' 'dbus'
        'fontconfig' 'libgl' 'libxrandr' 'libxv' 'libxi' 'alsa-lib'
        'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' ) #'libmng')
#makedepends=('postgresql-libs' 'mariadb' 'unixodbc' 'cups' 'gtk2' 'libfbclient'
makedepends=('postgresql-libs' 'unixodbc' 'cups' 'gtk2' 'libfbclient'
             'mesa')
optdepends=('qtchooser: set the default Qt toolkit'
            'postgresql-libs: PostgreSQL driver'
            'libmariadbclient: MariaDB driver'
            'unixodbc: ODBC driver'
            'libfbclient: Firebird/iBase driver'
            'libxinerama: Xinerama support'
            'libxcursor: Xcursor support'
            'libxfixes: Xfixes support'
            'icu: Unicode support'
            'sni-qt: StatusNotifierItem (AppIndicators) support')
install="${pkgname}.install"
replaces=('qt<=4.8.4')
conflicts=('qt')
options=('staticlibs') # libQtUiTools builds as static only
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/4.8/${pkgver}/${_pkgfqn}.tar.gz"
        'qtconfig-qt4.desktop' 'assistant-qt4.desktop' 'designer-qt4.desktop'
        'linguist-qt4.desktop' 'qdbusviewer-qt4.desktop'
        'improve-cups-support.patch'
        'moc-boost-workaround.patch'
        'CVE-2014-0190.patch'
        'kubuntu_14_systemtrayicon.diff'
        'symbol_visibility.patch'
        'static_webkit.patch')
md5sums=('2edbe4d6c2eff33ef91732602f3518eb'
         'a16638f4781e56e7887ff8212a322ecc'
         '8a28b3f52dbeb685d4b69440b520a3e1'
         '9727c406c240990870c905696a8c5bd1'
         '0e384663d3dd32abe35f5331c4147569'
         'b859c5673e5098c39f72b2252947049e'
         'c439c7731c25387352d8453ca7574971'
         'da387bde22ae1c446f12525d2a31f070'
         '34ed257109afb83342cfe514c8abe027'
         'a523644faa8f98a73f55c4aa23c114a6'
         '1083c6ece9228d80c8a3e202542c7662'
         'b6e14813ddf462a0da2a8b80de25110b')

prepare() {
  cd ${_pkgfqn}

  # (FS#28381) (KDEBUG#180051)
  patch -p1 -i "${srcdir}"/improve-cups-support.patch

  # QTBUG#22829
  patch -p1 -i "${srcdir}"/moc-boost-workaround.patch

  # QTBUG#38367
  patch -p1 -i "${srcdir}"/CVE-2014-0190.patch

  # http://blog.martin-graesslin.com/blog/2014/06/where-are-my-systray-icons/
  patch -p1 -i "${srcdir}"/kubuntu_14_systemtrayicon.diff

  sed -i "s|-O2|${CXXFLAGS}|" mkspecs/common/{g++,gcc}-base.conf
  sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS}|g" mkspecs/common/gcc-base.conf

  cp mkspecs/common/linux{,32}.conf
  sed -i "/^QMAKE_LIBDIR\s/s|=|= /usr/lib32|g" mkspecs/common/linux32.conf
  sed -i "s|common/linux.conf|common/linux32.conf|" mkspecs/linux-g++-32/qmake.conf

  patch -p1 -i "${srcdir}"/symbol_visibility.patch

  patch -p1 -i "${srcdir}"/static_webkit.patch
  # patch to cxxflags
  sed -i 's|QMAKE_CXXFLAGS += -Werror|QMAKE_CXXFLAGS += -Wundef|g' src/3rdparty/webkit/Source/WebKit.pri
  sed -i 's|exists($$PWD/WebKit/qt/tests): SUBDIRS += WebKit/qt/tests| |g' src/3rdparty/webkit/Source/WebKit.pro

}

build() {
  export QT4DIR="${srcdir}"/${_pkgfqn}
  export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

  cd ${_pkgfqn}

  sed -i '20 a QMAKE_LIBDIR += $$QT_BUILD_TREE/src/3rdparty/webkit/Source/JavaScriptCore/release' tools/assistant/tools/assistant/assistant.pro
  sed -i '21 a QMAKE_LIBDIR += $$QT_BUILD_TREE/src/3rdparty/webkit/Source/WebCore/release' tools/assistant/tools/assistant/assistant.pro

  ./configure -confirm-license -opensource \
    -prefix /usr \
    -bindir /usr/lib/qt4/bin \
    -headerdir /usr/include/qt4 \
    -docdir /usr/share/doc/qt4 \
    -plugindir /usr/lib/qt4/plugins \
    -importdir /usr/lib/qt4/imports \
    -datadir /usr/share/qt4 \
    -translationdir /usr/share/qt4/translations \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt4/examples \
    -demosdir /usr/share/doc/qt4/demos \
    -plugin-sql-{psql,mysql,sqlite,odbc,ibase} \
    -system-sqlite \
    -graphicssystem raster \
    -openssl-linked \
    -nomake demos \
    -nomake examples \
    -nomake docs \
    -silent \
    -no-rpath \
    -optimized-qmake \
    -reduce-relocations \
    -dbus-linked \
    -no-openvg  -static -webkit -phonon

#    -no-phonon \
#    -no-phonon-backend \
#    -no-webkit \

  # enable dynamic load plugin
  sed -i 's|defined(QT_SHARED)|1|g' src/corelib/plugin/qpluginloader.cpp
  sed -i 's|def QT_SHARED| 1|g'     src/corelib/plugin/qfactoryloader.cpp

  #sed -i 's| -lQtWebKit | -lQtWebKit -lwebcore -lQtWebKit |g' tools/assistant/tools/assistant/Makefile
  sed -i 's|-lwebcore | |g' tools/assistant/tools/assistant/Makefile
  sed -i 's|-ljscore | |g' tools/assistant/tools/assistant/Makefile

  cd src
  make
  cd ..
  ../../pack_all_a.sh libQtWebKit.a
  rm ./lib/libQtWebKit.a
  mv ./libQtWebKit.a ./lib/
  sed -i 's|-lwebcore | |g' ./lib/libQtWebKit.prl
  sed -i 's|-ljscore | |g'  ./lib/libQtWebKit.prl
  sed -i 's|-lwebcore | |g' ./lib/pkgconfig/QtWebKit.pc
  sed -i 's|-ljscore | |g'  ./lib/pkgconfig/QtWebKit.pc
  sed -i 's|-lwebcore | |g' ./lib/libQtWebKit.la
  sed -i 's|-ljscore | |g'  ./lib/libQtWebKit.la

  make
}

package() {
    cd ${_pkgfqn}
    make INSTALL_ROOT="${pkgdir}" install

    # install missing icons and desktop files
    install -D -m644 src/gui/dialogs/images/qtlogo-64.png \
      "${pkgdir}"/usr/share/icons/hicolor/64x64/apps/qt4logo.png
    install -D -m644 tools/assistant/tools/assistant/images/assistant.png \
      "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/assistant-qt4.png
    install -D -m644 tools/assistant/tools/assistant/images/assistant-128.png \
      "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/assistant-qt4.png
    install -D -m644 tools/designer/src/designer/images/designer.png \
      "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/designer-qt4.png
    for icon in tools/linguist/linguist/images/icons/linguist-*-32.png ; do
      size=$(echo $(basename ${icon}) | cut -d- -f2)
      install -D -m644 ${icon} \
          "${pkgdir}"/usr/share/icons/hicolor/${size}x${size}/apps/linguist-qt4.png
    done
    install -D -m644 tools/qdbus/qdbusviewer/images/qdbusviewer.png \
      "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/qdbusviewer-qt4.png
    install -D -m644 tools/qdbus/qdbusviewer/images/qdbusviewer-128.png \
      "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/qdbusviewer-qt4.png

    install -d "${pkgdir}"/usr/share/applications
    install -m644 "${srcdir}"/{assistant,designer,linguist,qtconfig,qdbusviewer}-qt4.desktop \
      "${pkgdir}"/usr/share/applications/

    # Useful symlinks for cmake and configure scripts
    install -d "${pkgdir}"/usr/bin
    for b in "${pkgdir}"/usr/lib/qt4/bin/*; do
      ln -s /usr/lib/qt4/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt4
    done

    # install license addition
    install -D -m644 LGPL_EXCEPTION.txt \
      ${pkgdir}/usr/share/licenses/${pkgname}/LGPL_EXCEPTION.txt

    # Fix wrong libs path in pkgconfig files
    find "${pkgdir}/usr/lib/pkgconfig" -type f -name '*.pc' \
      -exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;

    # Fix wrong bins path in pkgconfig files
    find "${pkgdir}/usr/lib/pkgconfig" -type f -name '*.pc' \
      -exec sed -i 's|/usr/bin/|/usr/lib/qt4/bin/|g' {} \;

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

    cp -f src/3rdparty/webkit/Source/JavaScriptCore/release/* ${pkgdir}/usr/lib/
    cp -f src/3rdparty/webkit/Source/WebCore/release/* ${pkgdir}/usr/lib/

    # The TGA plugin is broken (FS#33568)
    #rm "${pkgdir}"/usr/lib/qt4/plugins/imageformats/libqtga.so
    rm "$pkgdir"/usr/lib/qt4/bin/assistant
    rm "$pkgdir"/usr/lib/qt4/bin/designer
    rm "$pkgdir"/usr/lib/qt4/bin/linguist
    rm "$pkgdir"/usr/lib/qt4/bin/pixeltool
    rm "$pkgdir"/usr/lib/qt4/bin/qcollectiongenerator
    rm "$pkgdir"/usr/lib/qt4/bin/qdbus
    rm "$pkgdir"/usr/lib/qt4/bin/qdbuscpp2xml
    rm "$pkgdir"/usr/lib/qt4/bin/qdbusviewer
    rm "$pkgdir"/usr/lib/qt4/bin/qdbusxml2cpp
    rm "$pkgdir"/usr/lib/qt4/bin/qdoc3
    rm "$pkgdir"/usr/lib/qt4/bin/qhelpconverter
    rm "$pkgdir"/usr/lib/qt4/bin/qhelpgenerator
    rm "$pkgdir"/usr/lib/qt4/bin/qmlplugindump
    rm "$pkgdir"/usr/lib/qt4/bin/qmlviewer
    rm "$pkgdir"/usr/lib/qt4/bin/qt3to4
    rm "$pkgdir"/usr/lib/qt4/bin/qtconfig
    rm "$pkgdir"/usr/lib/qt4/bin/qttracereplay
    rm "$pkgdir"/usr/lib/qt4/bin/xmlpatterns
    rm "$pkgdir"/usr/lib/qt4/bin/xmlpatternsvalidator

    rm -f "$pkgdir"/usr/lib/libjscore.{a,prl}
    rm -f "$pkgdir"/usr/lib/libwebcore.{a,prl}

}
