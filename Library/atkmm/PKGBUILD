# $Id: PKGBUILD 79583 2010-05-04 15:21:15Z ibiru $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.24.1
pkgrel=1
arch=('i686' 'x86_64')
# makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++')
makedepends=('atk')
license=('LGPL')
options=('!libtool' '!emptydirs' 'staticlibs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/${pkgver:0:4}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('26c41d8da37d04eef9f219c6ce87d94852e1cacaad823050e520e1c08a36ed23')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  sed -i 's|-lglibmm_generate_extra_defs-2.4|& `pkg-config --libs glib-2.0` |g' ./codegen/Makefile.in

  ./configure --prefix=/usr --enable-static
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C doc DESTDIR="${pkgdir}" install
}
