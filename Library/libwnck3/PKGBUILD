# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
_pkgbasename=libwnck
pkgver=3.20.1
pkgrel=2
pkgdesc="Window Navigator Construction Kit (GTK+3)"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'startup-notification' 'libxres')
makedepends=('intltool' 'gobject-introspection' 'python2')
url="http://www.gnome.org/"
_commit=590b11f147fd31ef8e63187559e92358c8b28fc6  # tags/3.20.1^0
source=("git+https://git.gnome.org/browse/libwnck#commit=$_commit")
sha256sums=('SKIP')

options=('staticlibs')

pkgver() {
  cd libwnck
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libwnck

  # d-feet crash on Wayland
  git cherry-pick -n 40856004e394305c880124f3d3b5f08fc1e9a16d
  git cherry-pick -n c4cc2eb15f9da7a02ff584d5f517239b53a2c985

  NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "libwnck"

  export CFLAGS="-fPIC"
  export CPPFLAGS="-fPIC"
  export CXXFLAGS="-fPIC"

    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --enable-static
    make
}

package() {
    cd libwnck
    make DESTDIR="$pkgdir" install
}
