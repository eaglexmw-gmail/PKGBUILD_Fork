# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview3
_pkgbasename=gtksourceview
pkgver=3.18.2
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libxml2')
#makedepends=('intltool' 'gobject-introspection' 'glade' 'vala' 'python2')
makedepends=('intltool' 'gobject-introspection' 'vala' 'python2')
url="http://www.gnome.org"
options=('staticlibs')
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver:0:4}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('60f75a9f0039e13a2281fc595b5ef7344afa06732cc53b57d13234bfb0a5b7b2')

build() {
  cd "$_pkgbasename-$pkgver"

  export CFLAGS="-fPIC"
  export CPPFLAGS="-fPIC"
  export CXXFLAGS="-fPIC"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-static \
      --disable-glade-catalog --disable-shared
  make
}

package() {
  cd "$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
}
