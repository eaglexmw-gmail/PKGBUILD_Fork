# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer:  Maxime Gauduin <alucryd@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=('x264' 'libx264' 'libx264-10bit')
pkgver=148.20150725
pkgrel=1
epoch=2
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git' 'ffmpeg' 'l-smash')
_commit=73ae2d11d472d0eb3b7c218dc1659db32f649b14
source=(git://git.videolan.org/x264.git#commit=${_commit})
md5sums=('SKIP')

makedepends=('yasm' 'git')
options=('staticlibs')

pkgver() {
  cd x264
  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _date=$(git log -1 --format="%cd" --date=short | tr -d -)

  echo ${_ver}.${_date}
}

prepare() {
  cp -r x264 x264-10bit
}

build() {
  cd x264

  export CFLAGS="-fPIC"
  export CPPFLAGS="-fPIC"
  export CXXFLAGS="-fPIC"

  ./configure --prefix=/usr --enable-static --disable-shared --enable-pic

  make

  cd ../x264-10bit
  ./configure --prefix=/usr \
     --enable-static --disable-shared --enable-pic \
    --bit-depth=10
  make
}

package_x264() {
  pkgdesc='CLI tools for encoding H264/AVC video streams'
  depends=('ffmpeg' 'liblsmash.so')
  provides=('x264-10bit')
  conflicts=('x264-10bit')
  replaces=('x264-10bit')

  make -C $pkgbase DESTDIR="$pkgdir" install-cli
  install -Dm755 $pkgbase-10bit/x264 "$pkgdir"/usr/bin/x264-10bit
}

package_libx264() {
  pkgdesc='Library for encoding H264/AVC video streams'
  provides=('libx264.so' 'x264-dev')
  conflicts=('x264-dev')
  replaces=('x264-dev')

  install -d "$pkgdir"/usr/lib
  make -C $pkgbase DESTDIR="$pkgdir" install-lib-static
}

package_libx264-10bit() {
  pkgdesc='Library for encoding H264/AVC video streams. 10bit-depth.'
  provides=('libx264.so' 'libx264' 'x264-dev')
  conflicts=('libx264' 'x264-dev')

  install -d "$pkgdir"/usr/lib
  make -C $pkgbase-10bit DESTDIR="$pkgdir" install-lib-static
}
