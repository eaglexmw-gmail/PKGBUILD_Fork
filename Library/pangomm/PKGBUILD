# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.36.0
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
#makedepends=('pango' 'glibmm' 'glibmm-docs' 'cairomm' 'pkgconfig')
makedepends=('pango' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::4}/${pkgname}-${pkgver}.tar.xz)
options=('!libtool' 'staticlibs')
url="http://gtkmm.sourceforge.net/"
sha256sums=('a8d96952c708d7726bed260d693cece554f8f00e48b97cccfbf4f5690b6821f0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|-lglibmm_generate_extra_defs-2.4|& `pkg-config --libs gtk+-3.0` |g' ./tools/Makefile.in

  ./configure --prefix=/usr --enable-static
  make
}
package_pangomm() {
  pkgdesc="C++ bindings for pango"
  depends=('pango' 'glibmm' 'cairomm')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
