# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.7.2
pkgrel=3
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
#depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
#         'libtiff' 'zlib' 'ncurses')
depends=('boost-libs' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'clang35' 'llvm35')
options=('staticlibs')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz
        llvm-static-system-libs.patch)
md5sums=('f337bf8b973d38e5cea031c29175d93f'
         'b7c212b2549981ced03c091375f48122')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver
  #patch -Np1 < $srcdir/alignment.patch

  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  patch -Np1 < $srcdir/llvm-static-system-libs.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  export OPENIMAGEIOHOME=/usr/local

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVM_STATIC=1 \
    -DOSL_BUILD_CPP11=1 \
    -DCMAKE_CXX_FLAGS="\
      -Wno-error=array-bounds \
      -Wno-error=sign-compare \
      -Wno-error=strict-aliasing \
      -Wno-error=misleading-indentation" -DBUILDSTATIC=1 -DOSL_BUILD_TESTS=OFF

  #make cmake_check_build_system
  #sed -i 's|libOpenImageIO.a |& -lfreetype  -lOpenColorIO -lyaml-cpp -ltinyxml -lHalf -lIlmImf -lImath -lHalf -lIex -lIexMath -lIlmThread -pthread -ltiff -lpng16 -ljpeg -lgif -lopenjpeg -lwebp |g' src/testshade/CMakeFiles/testshade.dir/link.txt

  #sed -i 's|libOpenImageIO.a |& -lfreetype  -lOpenColorIO -lyaml-cpp -ltinyxml -lHalf -lIlmImf -lImath -lHalf -lIex -lIexMath -lIlmThread -pthread -ltiff -lpng16 -ljpeg -lgif -lopenjpeg -lwebp |g' src/testrender/CMakeFiles/testrender.dir/link.txt

  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir"/usr/share/OSL/
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/LICENSE "$pkgdir"/usr/share/licenses/$pkgname # TODO: Tell upstream about this shit
  mv "$pkgdir"/usr/{CHANGES,README.md,INSTALL} "$pkgdir"/usr/share/OSL/
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/OSL/doc
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders
}

# vim:set ts=2 sw=2 et:
