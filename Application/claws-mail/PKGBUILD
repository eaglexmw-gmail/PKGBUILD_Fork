# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=claws-mail
pkgver=3.14.0
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
depends=('gtk2' 'gnutls' 'startup-notification'  'gpgme'
         'libsm' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
#makedepends=('spamassassin' 'bogofilter' 'valgrind' 'intltool'
             # dependencies for plugins
#             'libsoup' 'libgdata' 'webkitgtk2' 'libnotify' 'libcanberra' 'poppler-glib' 'pygtk') 
             #'libchamplain' needed for geolocation plugin, disabled for now - most time it's not compatible with current gnome releases
makedepends=('intltool'
             # dependencies for plugins
             'libsoup' 'libnotify') 
             #'libchamplain' needed for geolocation plugin, disabled for now - most time it's not compatible with current gnome releases
optdepends=('python2:           needed for some tools and python plugin'
            'perl:              needed for some tools and perl plugin'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering'
            'libnotify:         for notification plugin'
            'libcanberra:       for notification plugin'
            'dbus:              for notification plugin'
            'libxml2:           for gtkhtml2_viewer and rssyl plugins'
            'curl:              for gtkhtml2_viewer, vcalendar, rssyl and spamreport plugins'
            'libarchive:        for archive plugin and various other plugins'
            'libytnef:          for tnef_parse plugin'
            'webkitgtk2:        for the fancy webkit html plugin'
            'libsoup:           for the fancy webkit html plugin'
	        'libgdata:          for gdata plugin'
	        'poppler-glib:      for pdf viewer plugin'
	        'ghostscript:       for pdf viewer plugin')    
replaces=('sylpheed-claws' 'claws-mail-extra-plugins')
conflicts=('claws-mail-extra-plugins')
provides=('claws')
install=claws-mail.install
source=(http://www.claws-mail.org/download.php?file=releases/claws-mail-$pkgver.tar.xz)
sha256sums=('466bd70350c69055d980dc84038d13f231f4ed060af899910e29c7a1de83cc59')

build() {
  cd ${pkgname}-${pkgver}
 
  # fixes for python2
  export PYTHON="/usr/bin/python2"
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  sed -i 's:python -c:python2 -c:g' configure
  
  ./configure --prefix=/usr/local/clawsmail --disable-static \
    --enable-enchant \
    --enable-gnutls \
    --enable-ldap \
    --enable-crash-dialog \
    --enable-pgpmime-plugin \
    --enable-spamassassin-plugin \
    --enable-bogofilter-plugin \
    --enable-appdata
    #--help
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 ${i} \
        ${pkgdir}/usr/local/clawsmail/lib/claws-mail/tools/${i}
  done
}
