# $Id$
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Maintainer: tobias [ tobias at archlinux org ]
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=vim
pkgname=('vim' 'gvim')
_topver=7.3
_patchlevel=401
__hgrev=0dabc2ce136c
_versiondir="vim${_topver//./}"
pkgver=${_topver}.${_patchlevel}
pkgrel=1
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
makedepends=('gpm' 'python2' 'libxt' 'desktop-file-utils' 'gtk2')
source=(ftp://ftp.archlinux.org/other/vim/${pkgname}-${pkgver}.tar.xz{,.sig}
        pythoncomplete.vim::http://www.vim.org/scripts/download_script.php\?src_id=10872
        vimrc archlinux.vim gvim.desktop)
sha1sums=('99498cb48c0c720ae9972a6cd33d4926bdf733b7'
          'ad14ce56f99ae1a86c96c5a5d0f44e48ad18a00e'
          '4d9dcfb32874aa5467e6f06e418aeb4e675daaf2'
          '3494baf53a63581ba69f86a81293640ff681c5c5'
          '25dd3c2ce436e73a367c8f73b68f7f6889682437'
          '4a579cf66590d711f49c5dfb4a25e5df116ff7ba')

# source PKGBUILD && mksource
mksource() {

  [[ -x /usr/bin/hg ]] || (echo "hg not found. Install mercurial." && return 1)

  __hgroot='http://vim.googlecode.com/hg/'
  __hgrepo='vim'
  __hgbranch='default'

  hg clone -b ${__hgbranch} -u ${__hgrev} "${__hgroot}${__hgrepo}" ${__hgrepo}

  pushd ${__hgrepo}
  if (( $(hg id -n) < $(hg id -nr ${__hgbranch}) )); then
    printf 'You are not building the latest revision!\n'
    printf "Consider updating __hgrev to $(hg id -r ${__hgbranch}).\n"
  fi
  popd

  mv vim ${pkgname}-${pkgver}
  find ${pkgname}-${pkgver} -depth -type d -name .hg -exec rm -rf {} \;
  rm ${pkgname}-${pkgver}/{.hgignore,.hgtags}
  tar -cJf ${pkgname}-${pkgver}.tar.xz ${pkgname}-${pkgver}/*
  rm -r ${pkgname}-${pkgver}
}

build() {
  cd "${srcdir}"

  cp -a ${pkgname}-${pkgver} vim-build

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
  # fix python name
  sed -i -e 's|vi_cv_path_python, python|vi_cv_path_python, python2|' \
    vim-build/src/configure.in
  (cd vim-build/src && autoconf)

  cp -a vim-build gvim-build

  cd "${srcdir}"/vim-build

  ./configure --prefix=/usr/local/gvim --localstatedir=/var/lib/vim \
    --with-features=big --with-compiledby=ArchLinux \
    --enable-gpm --enable-acl --with-x=no \
    --disable-gui --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp --disable-pythoninterp \
    --disable-python3interp --disable-rubyinterp --disable-luainterp

  make

  cd "${srcdir}"/gvim-build

  ./configure --prefix=/usr/local/gvim --localstatedir=/var/lib/vim \
    --with-features=big --with-compiledby=ArchLinux \
    --enable-gpm --enable-acl --with-x=yes \
    --enable-gui=gtk2 --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp --disable-pythoninterp \
    --disable-python3interp --disable-rubyinterp --disable-luainterp

  sed -i 's|"\$(VIMRCLOC)";|"\/usr\/share\/vim";|g' src/Makefile

  make
}

package_gvim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor (with advanced features, such as a GUI)'
  depends=("vim-runtime=${pkgver}-${pkgrel}" 'gpm' 'ruby' 'libxt'
           'desktop-file-utils' 'gtk2' 'lua' 'python2')
  provides=("vim=${pkgver}-${pkgrel}")
  conflicts=('vim')
  install=gvim.install

  cd "${srcdir}"/gvim-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}"/usr/local/gvim/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
  done

  # Move the runtime for later packaging
  mv "${pkgdir}"/usr/local/gvim/share/vim "${srcdir}"/runtime-install

  # freedesktop links
  install -Dm644 "${srcdir}"/gvim.desktop \
    "${pkgdir}"/usr/local/gvim/share/applications/gvim.desktop
  install -Dm644 runtime/vim48x48.png "${pkgdir}"/usr/local/gvim/share/pixmaps/gvim.png

  # license
   install -Dm644 "${srcdir}"/vim-${pkgver}/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/local/gvim/share/licenses/${pkgname}/license.txt
}


package_vim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
  depends=("vim-runtime=${pkgver}-${pkgrel}" 'gpm')
  conflicts=('gvim')

  cd "${srcdir}"/vim-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}"/usr/local/gvim/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/local/gvim/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
  rm -r "${pkgdir}"/usr/local/gvim/share/vim

  # license
  install -Dm644 "${srcdir}"/vim-${pkgver}/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/local/gvim/share/licenses/${pkgname}/license.txt
}
# vim:set sw=2 sts=2 et:
