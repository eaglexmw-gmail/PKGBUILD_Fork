# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=mate-panel
pkgname=${_pkgbase}
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The MATE Panel (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
#depends=('dbus-glib' 'dconf' 'gtk2' 'libwnck' 'libcanberra' 'libice'
#         'libmateweather>=1.10' 'librsvg' 'libsm' 'libsoup' 'libxau'
#         'mate-menus>=1.10' 'mate-desktop>=1.10')
depends=('dbus-glib' 'dconf' 'gtk2' 'libwnck' 'libice'
         'librsvg' 'libsm' 'libsoup' 'libxau')
makedepends=('intltool' 'itstool' 'gobject-introspection')
optdepends=('yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate')
conflicts=("${_pkgbase}-gtk3")
sha1sums=('799ef0b7d718e2120aa1ffdcae604c356213ef12')
install=${_pkgbase}.install

if [ "$CARCH" = "x86_64" ]; then
  dstdir=mate64
elif [ "$CARCH" = "i686" ]; then
  dstdir=mate
fi

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"

    export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/mate_gtk+2.0/lib/pkgconfig/

    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+2.0 \
        --libexecdir=/usr/local/${dstdir}/mate_gtk+2.0/lib/${_pkgbase} \
        --sysconfdir=/usr/local/${dstdir}/mate_gtk+2.0/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
