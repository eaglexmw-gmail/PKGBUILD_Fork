# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=mate-session-manager
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The MATE Session Handler (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
#depends=('dbus-glib' 'gtk3' 'libsm' 'libxtst' 'mate-desktop-gtk3>=1.12'
#         'mate-polkit-gtk3>=1.12' 'mate-settings-daemon-gtk3>=1.10' 'ttf-dejavu'
#         'xdg-user-dirs')
depends=('dbus-glib' 'gtk3' 'libsm' 'libxtst' 'ttf-dejavu' 'xdg-user-dirs')
makedepends=('intltool' 'xtrans' 'systemd')
optdepends=('lightdm-gtk-greeter: A lightweight display manager'
            'onboard: On-screen keyboard useful for mobility impaired users'
            'orca: Screen reader for blind or visually impaired individuals'
            'xdg-user-dirs-gtk: Add autostart that prompts to rename user directories when switching locales')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('510d4ffd3b15fddfefeae3c126ca23b31166bcb1')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=mate64
elif [ "$CARCH" = "i686" ]; then
  dstdir=mate
fi

build() {
    cd ${_pkgbase}-${pkgver}

    export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/mate_gtk+3.0/lib/pkgconfig/

    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+3.0 \
        --libexecdir=/usr/local/${dstdir}/mate_gtk+3.0/lib/${_pkgbase} \
        --sysconfdir=/usr/local/${dstdir}/mate_gtk+3.0/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-upower
    make
}

package() {
    cd ${_pkgbase}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
