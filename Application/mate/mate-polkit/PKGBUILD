# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=mate-polkit
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2' 'gtk3' 'polkit')
makedepends=('gobject-introspection' 'mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('ccdb689c4baa09aba251662b31e016aa233e7191')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=mate64
elif [ "$CARCH" = "i686" ]; then
  dstdir=mate
fi

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+2.0 \
        --libexecdir=/usr/local/${dstdir}/mate_gtk+2.0/lib/${pkgbase} \
        --sysconfdir=/usr/local/${dstdir}/mate_gtk+2.0/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+3.0 \
        --libexecdir=/usr/local/${dstdir}/mate_gtk+3.0/lib/${pkgbase} \
        --sysconfdir=/usr/local/${dstdir}/mate_gtk+3.0/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-introspection \
        --disable-static
    make
}

package_mate-polkit() {
    pkgdesc+=' (GTK2 version)'
    groups=('mate')
    conflicts=("${pkgbase}-gtk3")
    depends=('gtk2' 'polkit')

    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_mate-polkit-gtk3() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    groups=('mate-gtk3')
    conflicts=("${pkgbase}")
    depends=('gtk3' 'polkit')

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
