# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.16
pkgbase=libmateweather
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2' 'gtk3')
makedepends=('intltool' 'libsoup')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('912eb67104da9414eb47c4a9b0631735d2528794')
install=${pkgbase}.install

if [ "$CARCH" = "x86_64" ]; then
  dstdir=mate64
elif [ "$CARCH" = "i686" ]; then
  dstdir=mate
fi

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+2.0 \
        --sysconfdir=/usr/local/${dstdir}/mate_gtk+2.0/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static \
        --disable-python \
        --enable-locations-compression
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+3.0 \
        --sysconfdir=/usr/local/${dstdir}/mate_gtk+3.0/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-python \
        --enable-locations-compression
    make
}

package_libmateweather() {
    pkgdesc+=' (GTK2 version)'
    conflicts=("${pkgbase}-gtk3")
    depends=('dconf' 'gtk2' 'libsoup')
    
    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_libmateweather-gtk3() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    conflicts=("${pkgbase}")
    depends=('dconf' 'gtk3' 'libsoup')
    
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
