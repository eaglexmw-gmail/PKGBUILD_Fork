# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=pluma
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.0
pkgrel=3
pkgdesc="A powerful text editor for MATE (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
#depends=('desktop-file-utils' 'enchant' 'gtk3' 'gtksourceview3' 'iso-codes'
#         'libsm' 'mate-desktop-gtk3>=1.12' 'zenity')
depends=('desktop-file-utils' 'gtk3' 'iso-codes' 'libsm')
makedepends=('intltool' 'itstool')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('14a0497fade1e3fd40a39eafb28390975abd12a6')


if [ "$CARCH" = "x86_64" ]; then
  dstdir=mate64
elif [ "$CARCH" = "i686" ]; then
  dstdir=mate
fi

prepare() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    sed -i 's/python/python2/' plugins/externaltools/data/switch-c.tool.in
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"

    export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/mate_gtk+3.0/lib/pkgconfig/

    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+3.0 \
        --libexecdir=/usr/local/${dstdir}/mate_gtk+3.0/lib/${_pkgbase} \
        --with-gtk=3.0 \
        --enable-gtk-doc=no \
        --disable-python
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
