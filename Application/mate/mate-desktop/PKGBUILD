# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.16
pkgbase=mate-desktop
pkgname=(${pkgbase} ${pkgbase}-gtk3 ${pkgbase}-schemas ${pkgbase}-schemas-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'startup-notification')
makedepends=('gobject-introspection' 'intltool')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('bcded93e383e810817e9737ce2796050f9038b2a')
install=${pkgbase}.install

if [ "$CARCH" = "x86_64" ]; then
  dstdir=mate64
elif [ "$CARCH" = "i686" ]; then
  dstdir=mate
fi

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+2.0 \
        --with-gtk=2.0 \
        --disable-static \
        --enable-schemas-compile \
        --disable-gtk-doc
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr/local/${dstdir}/mate_gtk+3.0 \
        --with-gtk=3.0 \
        --disable-static \
        --enable-schemas-compile \
        --disable-gtk-doc
    make
}

package_mate-desktop() {
    pkgdesc+=' (GTK2 version)'
    groups=('mate')
    conflicts=("${pkgbase}-gtk3")
    depends=('dconf' 'gtk2' 'startup-notification')
    
    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_mate-desktop-gtk3() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    groups=('mate-gtk3')
    conflicts=("${pkgbase}")
    depends=('dconf' 'gtk3' 'startup-notification')
    
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}

package_mate-desktop-schemas() {
    pkgdesc='GSettings schemas for MATE Desktop'
    depends=()

    cd "${srcdir}/${pkgbase}-gtk2"
    make -C schemas DESTDIR="${pkgdir}" install
}


package_mate-desktop-schemas-gtk3() {
    pkgdesc='GSettings schemas for MATE Desktop'
    depends=()

    cd "${srcdir}/${pkgbase}-gtk3"
    make -C schemas DESTDIR="${pkgdir}" install
}

