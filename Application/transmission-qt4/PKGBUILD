# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

_pkgbase=transmission
pkgname=${_pkgbase}-qt4
pkgver=2.84
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (Qt GUI)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
#depends=('curl' 'qt4' 'libevent')
depends=('curl' 'libevent')
#makedepends=('gtk3' 'intltool' 'curl' 'qt4' 'libevent')
makedepends=('gtk3' 'intltool' 'curl' 'libevent')
optdepends=('transmission-cli: daemon and web support')
provides=('transmission-qt')
conflicts=('transmission-qt')
install=transmission-qt.install
source=(http://mirrors.m0k.org/transmission/files/${_pkgbase}-${pkgver}.tar.xz
        "qt4-c++11.patch")
sha256sums=('a9fc1936b4ee414acc732ada04e84339d6755cd0d097bcbd11ba2cfc540db9eb'
            'cf0a6edd348da3d2d21dd7486084b67b8ae2b073c0d9699688591e17853dd15c')

prepare() {
  cd ${_pkgbase}-${pkgver}

  patch -Np0 -i "${srcdir}/qt4-c++11.patch"
}

build() {
  cd ${_pkgbase}-${pkgver}

  ./configure --prefix=/usr/local/transmission
  make
  pushd qt
  qmake qtr.pro

  sed -i 's|-lpng15 |-lpng14 |g' Makefile
  sed -i 's|-lpng |-lpng14 |g' Makefile

  make QMAKE_CXXFLAGS+=-std=c++11
}

package() {
  cd ${_pkgbase}-${pkgver}

  make -C qt INSTALL_ROOT="${pkgdir}"/usr/local/transmission install

  install -D -m644 COPYING "${pkgdir}/usr/local/transmission/share/licenses/transmission-qt/COPYING"
  install -D -m644 qt/icons/transmission.png "${pkgdir}/usr/local/transmission/share/pixmaps/transmission-qt.png"
  install -D -m644 qt/transmission-qt.desktop "${pkgdir}/usr/local/transmission/share/applications/transmission-qt.desktop"
}

# vim: set ts=2 sw=2 ft=sh noet:
