# $Id$
# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>

pkgbase=transmission
pkgname=('transmission-cli' 'transmission-gtk' 'transmission-qt')
pkgver=2.84
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
#makedepends=('gtk3' 'intltool' 'curl' 'qt5-base' 'libevent')
makedepends=('gtk3' 'intltool' 'curl' 'libevent')
source=(http://mirrors.m0k.org/transmission/files/$pkgbase-$pkgver.tar.xz
        qt4-c++11.patch)
md5sums=('411aec1c418c14f6765710d89743ae42'
         '085e5667f46d44e3d1eebd5badb0e218')

prepare() {
  cd ${pkgbase}-${pkgver}

  patch -Np0 -i "${srcdir}/qt4-c++11.patch"
}

build() {
  cd $pkgbase-$pkgver

  ./configure --prefix=/usr/local/transmission
  make
  pushd qt
  qmake qtr.pro
  sed -i 's|-lpng15 |-lpng14 |g' Makefile
  sed -i 's|-lpng |-lpng14 |g' Makefile
  make
}

package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent' 'systemd')
  install=transmission-cli.install

  cd $pkgbase-$pkgver

  for dir in daemon cli web utils
  do
      make -C "$dir" DESTDIR="$pkgdir" install
  done

  install -D -m644 "$srcdir/transmission-$pkgver/daemon/transmission-daemon.service" "$pkgdir/usr/local/transmission/lib/systemd/system/transmission.service"
  install -D -m644 COPYING "$pkgdir/usr/local/transmission/share/licenses/transmission-cli/COPYING"
}

package_transmission-gtk() {
  pkgdesc="Fast, easy, and free BitTorrent client (GTK+ GUI)"
  #depends=('curl' 'libevent' 'gtk3' 'desktop-file-utils' 'hicolor-icon-theme')
  depends=('curl' 'libevent' 'desktop-file-utils' 'hicolor-icon-theme')
  optdepends=('notification-daemon: Desktop notification support'
  	      'transmission-cli: daemon and web support')
  install=transmission-gtk.install

  cd $pkgbase-$pkgver

  make -C gtk DESTDIR="$pkgdir" install
  make -C po DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/local/transmission/share/licenses/transmission-gtk/COPYING"
}

package_transmission-qt() {
  pkgdesc="Fast, easy, and free BitTorrent client (Qt GUI)"
  depends=('curl' 'qt5-base' 'libevent' 'libxkbcommon-x11')
  optdepends=('transmission-cli: daemon and web support')
  install=transmission-qt.install

  cd $pkgbase-$pkgver

  make -C qt INSTALL_ROOT="$pkgdir"/usr/local/transmission install

  install -D -m644 COPYING "$pkgdir/usr/local/transmission/share/licenses/transmission-qt/COPYING"
  install -D -m644 qt/icons/transmission.png "$pkgdir/usr/local/transmission/share/pixmaps/transmission-qt.png"
  install -D -m644 qt/transmission-qt.desktop "$pkgdir/usr/local/transmission/share/applications/transmission-qt.desktop"
}
