# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=7.8.1
pkgrel=1
pkgdesc="The GNU Debugger"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
#depends=('ncurses' 'expat' 'python2' 'xz')
depends=('ncurses' 'expat' 'xz')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz)
md5sums=('8072be87a94be0936bc3b4b6941b0862')

prepare() {
  cd ${pkgname}-${pkgver}

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd ${pkgname}-${pkgver}
  
  # guile support has a severe bug https://sourceware.org/bugzilla/show_bug.cgi?id=17247
  ./configure --prefix=/usr/local/gdb --disable-nls \
    --with-system-readline \
    --without-python \
    --with-system-gdbinit=/usr/local/gdb/etc/gdb/gdbinit \
    --without-guile
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install "custom" system gdbinit
  install -dm755 $pkgdir/usr/local/gdb/etc/gdb
  touch $pkgdir/usr/local/gdb/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm ${pkgdir}/usr/local/gdb/include/{ansidecl,bfd,bfdlink,dis-asm,symcat}.h
  rm ${pkgdir}/usr/local/gdb/share/info/{bfd,configure,standards}.info
}
