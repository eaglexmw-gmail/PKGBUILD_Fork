# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=7.9.1
pkgrel=1
pkgdesc='The GNU Debugger'
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
#depends=('ncurses' 'expat' 'python2' 'xz')
depends=('ncurses' 'expat' 'xz')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz)
sha1sums=('04ba2906279b16b5f99c4f6b25942843a3717cdb')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker

if [ "$CARCH" = "x86_64" ]; then
  dstdir=mate64
elif [ "$CARCH" = "i686" ]; then
  dstdir=mate
fi

prepare() {
  cd gdb-$pkgver

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb-$pkgver
  
  # guile support has a severe bug https://sourceware.org/bugzilla/show_bug.cgi?id=17247
  ./configure --prefix=/usr/local/${dstdir} --disable-nls \
    --with-system-readline \
    --without-python \
    --with-system-gdbinit=/usr/local/${dstdir}/etc/gdb/gdbinit \
    --without-guile
  make
}

package() {
  cd gdb-$pkgver
  make DESTDIR=$pkgdir install

  # install "custom" system gdbinit
  install -dm755 $pkgdir/usr/local/${dstdir}/etc/gdb
  touch $pkgdir/usr/local/${dstdir}/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm ${pkgdir}/usr/local/${dstdir}/include/{ansidecl,bfd,bfdlink,dis-asm,symcat}.h
  rm ${pkgdir}/usr/local/${dstdir}/share/info/bfd.info
  rm $pkgdir/usr/local/${dstdir}/lib/{libbfd,libopcodes}.a
}
