# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: geekitus

pkgname=blobwars
pkgver=1.19
pkgrel=5
pkgdesc="Platform action game featuring a blob wih lot of weapons"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/blobwars"
license=('GPL')
#makedepends=('sdl_image' 'sdl_mixer' 'sdl_ttf' 'sdl_net')
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('87e8aaa114b6e4742ea6ee28e7a7f984')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix building with gcc 4.6
  sed -i -e 's/-Werror/-Werror -Wno-error=unused-result/g' makefile

  sed -i 's|-lSDL_mixer -lSDL_image -lSDL_ttf |-lSDL_mixer -lsmpeg -lmikmod -lSDL -lSDL_image -ljpeg -ltiff -lpng -lwebp -lSDL_ttf -lfreetype |g' makefile

  sed -i 's| $(LIBS) $(GAMEOBJS) | $(GAMEOBJS) $(LIBS) |g' makefile
  
  sed -i -e 's|/usr|/usr/local/blobwars|g' \
         -e 's|(PREFIX)/games/|(PREFIX)/bin|g' \
         -e 's|/share/games/blobwars|/share/blobwars|g' \
          makefile

  RELEASE=1 make 
}

package() {
  depends=('hicolor-icon-theme' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'sdl_net' 'blobwars-data>=1.19')

  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX="${pkgdir}/usr/local/blobwars" DATADIR="${pkgdir}/usr/local/blobwars/share/blobwars" BINDIR="${pkgdir}/usr/local/blobwars/bin/" install

  #make install

  # We remove data files (See blobwars-data)
  rm -r "${pkgdir}/usr/local/blobwars/share/blobwars"

  chmod a+r "${pkgdir}/usr/local/blobwars/share/applications/${pkgname}.desktop"
  chmod a+r -R "${pkgdir}/usr/local/blobwars/share/icons/hicolor/"
  install -dm755 "${pkgdir}/usr/local/blobwars/share/pixmaps"
  install -m644 icons/blobwars.png "${pkgdir}/usr/local/blobwars/share/pixmaps/"
}
