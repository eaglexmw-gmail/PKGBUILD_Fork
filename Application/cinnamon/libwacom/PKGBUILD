# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libwacom
pkgver=0.24
pkgrel=1
pkgdesc="Library to identify Wacom tablets and their features"
arch=('x86_64' 'i686')
url="http://sourceforge.net/apps/mediawiki/linuxwacom/index.php?title=Libwacom"
license=('MIT')
depends=('glib2' 'systemd' 'libgudev')
source=(https://sourceforge.net/projects/linuxwacom/files/libwacom/$pkgname-$pkgver.tar.bz2)
sha1sums=('74d38e416fc7b91bbf3e163d40fe6ff688e6da51')
sha256sums=('2905a0ee5cc85eceb9f5bd06109e66a051b0ed18102c3e6798717c259c8d66c3')


if [ "$CARCH" = "x86_64" ]; then
  dstdir=cinnamon64
elif [ "$CARCH" = "i686" ]; then
  dstdir=cinnamon
fi

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr/local/${dstdir}
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}/usr/local/${dstdir}/share/licenses/${pkgname}/LICENSE"
  install -m755 -d ${pkgdir}/usr/local/${dstdir}/lib/udev/rules.d
  cd tools
  ./generate-udev-rules > ${pkgdir}/usr/local/${dstdir}/lib/udev/rules.d/65-libwacom.rules
}
