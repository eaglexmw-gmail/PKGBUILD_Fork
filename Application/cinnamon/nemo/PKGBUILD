# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo
pkgver=3.2.2
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
#depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
#         'cinnamon-desktop' 'libnotify' 'libxml2' 'cinnamon-translations')
depends=('libexif' 'dconf' 'desktop-file-utils'
         'libnotify' 'libxml2' )
#makedepends=('gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common' 'python2-gobject' 'python2-polib')
makedepends=('gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common' 'python2-gobject' 'python2-polib')
options=('!emptydirs')
install=nemo.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver" locale.patch)
sha256sums=('5f04fe37112d15d55d0fe1783252a8d03df70897fb5349ef6d28dbdbcf226407' 'SKIP')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=cinnamon64
elif [ "$CARCH" = "i686" ]; then
  dstdir=cinnamon
fi

prepare() {
  cd linuxmint-nemo-*

  patch -Np1 -i ../locale.patch

  # Python2 fix
  # find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # directory: limit deep scount (folder contents and size) to one filesystem (FS#47480)
  # https://github.com/linuxmint/nemo/pull/1083
  # patch -Np1 -i ../deep-count-one-filesystem.patch

  # Fix fallback style for GTK 3.20
  # https://github.com/linuxmint/nemo/pull/1138
  # patch -Np1 -i ../0001-Fix-fallback-style-for-GTK-3.20.patch

  # Rename 'Files' app name to avoid having the same as nautilus
  sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in
}

build() {
  cd linuxmint-nemo-*

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/

  ./autogen.sh --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/local/${dstdir}/lib/nemo \
      --disable-update-mimedb \
      --disable-tracker \
      --disable-gtk-doc-html \
      --disable-schemas-compile

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd linuxmint-nemo-*

  make DESTDIR="$pkgdir" install
}
