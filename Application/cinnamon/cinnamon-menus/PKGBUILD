#Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>

pkgname=cinnamon-menus
pkgver=3.2.0
pkgrel=1
pkgdesc="Cinnamon menu specifications"
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection' 'python2' 'gnome-common')
license=('GPL' 'LGPL')
url="https://github.com/linuxmint/cinnamon-menus"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-menus/archive/${pkgver}.tar.gz")
sha256sums=('96eb3303151fabc18ccf2719849149d08f7391d22d0a6b72e82326009bb243b5')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=cinnamon64
elif [ "$CARCH" = "i686" ]; then
  dstdir=cinnamon
fi

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
      --localstatedir=/var --disable-static \
      --sbindir=/usr/bin
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
