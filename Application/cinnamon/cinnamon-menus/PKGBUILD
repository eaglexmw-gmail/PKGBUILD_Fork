#Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>

pkgname=cinnamon-menus
pkgver=3.0.0
pkgrel=1
pkgdesc="Cinnamon menu specifications"
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection' 'python2' 'gnome-common')
license=('GPL' 'LGPL')
url="https://github.com/linuxmint/cinnamon-menus"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-menus/archive/${pkgver}.tar.gz")
sha256sums=('88894d8a5128573771085bcea58b89964755a18255edeab12d14e30cd3b24eb0')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=cinnamon64
elif [ "$CARCH" = "i686" ]; then
  dstdir=cinnamon
fi

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
      --localstatedir=/var --disable-static \
      --sbindir=/usr/bin
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
