# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgbase=networkmanager
pkgname=(networkmanager libnm-glib)
pkgver=1.0.10
pkgrel=1
pkgdesc="Network Management daemon"
arch=(i686 x86_64 pentium4)
license=(GPL2 LGPL2.1)
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.7
#makedepends=(intltool dhclient iptables gobject-introspection gtk-doc "ppp=$_pppver"
#             modemmanager dbus-glib iproute2 libnl nss polkit wpa_supplicant libsoup
#             systemd libgudev libmm-glib rp-pppoe libnewt libndp libteam vala perl-yaml
#             python2-gobject)
makedepends=(intltool dhclient iptables gobject-introspection gtk-doc
             dbus-glib iproute2 libnl nss polkit wpa_supplicant libsoup
             systemd libgudev rp-pppoe vala perl-yaml
             )
checkdepends=(libx11 python2-dbus)
checkdepends=(libx11)
#_commit=ba46efd07777350be737dbdac9df9becb86e8f77
#source=("git://anongit.freedesktop.org/NetworkManager/NetworkManager#commit=$_commit"
source=(http://ftp.gnome.org/pub/gnome/sources/NetworkManager/${pkgver:0:3}/NetworkManager-$pkgver.tar.xz
        NetworkManager.conf disable_set_hostname.patch)
sha256sums=('1bcfce8441dfd9f432a100d06b54f3831a2275cccc3b74b1b4c09a011e179fbc'
            '2c6a647b5aec9f3c356d5d95251976a21297c6e64bd8d2a59339f8450a86cb3b'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=cinnamon64
elif [ "$CARCH" = "i686" ]; then
  dstdir=cinnamon
elif [ "$CARCH" = "pentium4" ]; then
  dstdir=cinnamon
fi

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python

  cd NetworkManager-$pkgver
  patch -Np1 -i ../disable_set_hostname.patch
  NOCONFIGURE=1 ./autogen.sh
  sed -i '127,129d' src/dhcp-manager/systemd-dhcp/nm-sd-adapt.h
  sed -i '127a#define gettid() ((pid_t)syscall(SYS_gettid))' src/dhcp-manager/systemd-dhcp/nm-sd-adapt.h
}

build() {
  export PATH="$srcdir/path:$PATH"

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  cd NetworkManager-$pkgver
if [ "$CARCH" = "x86_64" ]; then
  export LDFLAGS="-L/usr/local/lib64"
else
  export LDFLAGS="-L/usr/local/lib32"
fi 
  ./configure --prefix=/usr/local/${dstdir} \
    --sysconfdir=/usr/local/${dstdir}/etc \
    --localstatedir=/var \
    --sbindir=/usr/local/${dstdir}/bin \
    --libexecdir=/usr/local/${dstdir}/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --without-dhcpcd \
    --with-dnsmasq=/usr/bin/dnsmasq \
    --with-iptables=/usr/bin/iptables \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-pppd=/usr/bin/pppd \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-pppoe=/usr/bin/pppoe \
    --with-kernel-firmware-dir=/usr/lib/firmware \
    --with-session-tracking=systemd \
    --without-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --enable-doc \
    --enable-gtk-doc

  make
}

check() {
  export PATH="$srcdir/path:$PATH"
  cd NetworkManager-$pkgver
  # make -k check
}

package_networkmanager() {
  depends=(libnm-glib iproute2 libnl polkit wpa_supplicant dhclient libsoup
           libmm-glib libnewt libndp libteam libgudev)
  optdepends=('dnsmasq: connection sharing'
              'bluez: Bluetooth support'
              'openresolv: resolvconf support'
              'ppp: dialup connection support'
              'rp-pppoe: ADSL support'
              'modemmanager: cellular network support')
  install=networkmanager.install
  backup=('etc/NetworkManager/NetworkManager.conf')

  cd NetworkManager-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libnm uninstall
  make DESTDIR="$pkgdir" -C libnm-glib uninstall
  make DESTDIR="$pkgdir" -C libnm-util uninstall
  make DESTDIR="$pkgdir" -C vapi uninstall

  # Some stuff to move is left over
  mv "$pkgdir/usr/local/${dstdir}/include" ..
  mv "$pkgdir/usr/local/${dstdir}/lib/pkgconfig" ..

  install -m644 ../NetworkManager.conf "$pkgdir/usr/local/${dstdir}/etc/NetworkManager/"
  install -m755 -d "$pkgdir/usr/local/${dstdir}/etc/NetworkManager/dnsmasq.d"

  rm -r "$pkgdir/var/run"
}

package_libnm-glib() {
  pkgdesc="NetworkManager library"
  depends=(libgudev nss dbus-glib libutil-linux)

  install -d "$pkgdir/usr/local/${dstdir}/lib"
  mv include "$pkgdir/usr/local/${dstdir}"
  mv pkgconfig "$pkgdir/usr/local/${dstdir}/lib"

  cd NetworkManager-$pkgver
  make DESTDIR="$pkgdir" -C libnm install
  make DESTDIR="$pkgdir" -C libnm-util install
  make DESTDIR="$pkgdir" -C libnm-glib install
  make DESTDIR="$pkgdir" -C vapi install
}
