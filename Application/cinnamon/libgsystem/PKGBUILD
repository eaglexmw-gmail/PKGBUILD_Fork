# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgsystem
pkgver=2015.2+4+gd606bec
pkgrel=1
pkgdesc='"Copylib" for system service modules using GLib with GCC'
url="https://wiki.gnome.org/Projects/LibGSystem"
license=(GPL2)
arch=(i686 x86_64 pentium4)
depends=(glib2 libsystemd)
makedepends=(gobject-introspection gtk-doc systemd)
_commit=d606bec68ddfea78de4b03c3f3568afb71bdc1ce  # master
#source=("git+https://git.gnome.org/browse/libgsystem#commit=$_commit"
#        "git+https://git.gnome.org/browse/libglnx")
source=("git+https://gitlab.gnome.org/Archive/libgsystem.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libglnx.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix header file location
  sed -i 's|attr/xattr|sys/xattr|g' configure.ac

  git submodule init
  git config submodule.externals/vendor/libglnx.url "$srcdir/libglnx"
  git -c protocol.file.allow=always submodule update

  NOCONFIGURE=1 ./autogen.sh
}

if [ "$CARCH" = "x86_64" ]; then
  dstdir=cinnamon64
elif [ "$CARCH" = "i686" ]; then
  dstdir=cinnamon
elif [ "$CARCH" = "pentium4" ]; then
  dstdir=cinnamon
fi

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr/local/${dstdir} --disable-static
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
