# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgsystem
pkgver=2015.2
pkgrel=1
pkgdesc='"Copylib" for system service modules using GLib with GCC'
url="https://wiki.gnome.org/Projects/LibGSystem"
license=(GPL2)
arch=(i686 x86_64)
depends=(glib2 libsystemd)
makedepends=(gobject-introspection gtk-doc systemd)
source=("git://git.gnome.org/libgsystem#tag=v$pkgver"
        "git://git.gnome.org/libglnx")
sha256sums=('SKIP'
            'SKIP')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=cinnamon64
elif [ "$CARCH" = "i686" ]; then
  dstdir=cinnamon
fi

prepare() {
  cd $pkgname
  git submodule init
  git config --local submodule.libglnx.url "$srcdir/libglnx"
  git submodule update
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr/local/${dstdir} --disable-static
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
