# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Denis (dtonator@gmail.com)

pkgname=xmoto
pkgver=0.5.11
pkgrel=2
pkgdesc="A challenging 2D motocross platform game, where physics play an important role."
arch=('i686' 'x86_64')
url="http://xmoto.tuxfamily.org"
license=('GPL')
#depends=('libjpeg' 'libpng' 'lua52' 'sdl_mixer' 'ode' 'curl' 'sqlite' 'sdl_ttf'
#         'desktop-file-utils' 'sdl_net' 'glu' 'libxdg-basedir' 'libxml2')
depends=('libjpeg' 'libpng' 'curl' 'sqlite'
         'desktop-file-utils' 'glu' 'libxml2')
makedepends=('mesa')
install=xmoto.install
source=("http://download.tuxfamily.org/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        'xmoto-0.5.10-libpng15.patch'
        'lua52_compat.patch'
        'system.cpp.patch')
sha1sums=('029c24eaf2bfe20dbb870e2f2173e2a0a93f3a1e'
          '9d13fa09f1c558a0fc504f7e7b1ceac6b4b7b20d'
          '104542546fbbd24172ddaa179f87e1b26df8b761'
          'e741f769d1fc5779a6688970ee97e2a61a50c7ae')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}+dfsg.orig"

  patch -Np1 -i "${srcdir}/xmoto-0.5.10-libpng15.patch"
  patch -Np1 -i "${srcdir}/system.cpp.patch"
  patch -Np1 -i "${srcdir}/lua52_compat.patch"

  # Remove bundled ode library because its headers get picked up during build
  rm -r src/ode

  sed -i 's|"../glext.h"|<GL/glext.h>|g' src/include/xm_OpenGL.h
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}+dfsg.orig"

  ./bootstrap # regenrate confif file because the lua patch modifies configure.in

  sed -i 's|-lSDL_image|-L/usr/local/lib -lSDL_image -lpng -ltiff -ljpeg -lSDL -lfreetype -lX11 -lpthread -ldl -lz |g' configure
  sed -i 's|"-lSDL_mixer |"-L/usr/local/lib -lSDL_mixer -lsmpeg -lstdc++ -lSDL -lfreetype -lX11 -lpthread -ldl -lz  -lvorbis -logg -lmikmod -lSDL -lfreetype -lX11 -lpthread -ldl -lz |g' configure

  ./configure --prefix=/usr/local/xmoto --disable-sdltest --with-internal-xdg=1  --with-asian-ttf-file=/usr/share/fonts/wenquanyi/wqy-zenhei/wqy-zenhei.ttc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}+dfsg.orig"
  make DESTDIR="${pkgdir}" mangdir=/usr/local/xmoto/share/man/man6 install

  # install desktop file
  install -Dm0644 "${srcdir}/${pkgname}-${pkgver}+dfsg.orig/extra/xmoto.desktop" \
        "${pkgdir}/usr/local/xmoto/share/applications/xmoto.desktop"

  # install icon for desktop file
  install -Dm0644 "${srcdir}/${pkgname}-${pkgver}+dfsg.orig/extra/xmoto.xpm" \
        "${pkgdir}/usr/local/xmoto/share/pixmaps/xmoto.xpm"
}
