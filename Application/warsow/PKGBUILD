# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: Babets
# Contributor: Vain

pkgname=warsow
pkgver=1.51
pkgrel=1
pkgdesc="Free online multiplayer competitive FPS based on the Qfusion engine"
url="http://www.warsow.net/"
license=('GPL')
arch=('i686' 'x86_64')
#depends=('curl' 'libjpeg' 'libvorbis' 'libxinerama' 'libxxf86dga' 'libxxf86vm'
#         'sdl' 'warsow-data' 'libxrandr' 'libpng' 'libtheora' 'freetype2' 'libxi')
depends=('curl' 'libjpeg' 'libvorbis' 'libxinerama' 'libxxf86dga' 'libxxf86vm'
         'libxrandr' 'libpng' 'freetype2' 'libxi')
#makedepends=('mesa' 'openal' 'imagemagick' 'gendesk')
makedepends=('mesa' 'gendesk')
optdepends=('openal: for openal audio support')
source=('warsow.launcher' 'wsw-server.launcher' 'wswtv-server.launcher' \
        "http://www.warsow.eu/warsow_1.51_sdk.tar.gz" 'warsow.png')
_genericname=('First person shooter')
_comment=('Online Mulitiplayer Competitive FPS')
md5sums=('13327ba27272a3b751c7171d364c831e'
         '3bed4cf8187b55e0711b784b784d4a55'
         '65c4760360505ba4498549841455c12a'
         '0dc21fbc33cfe13b5f038cdd20139f5c'
         'c2020d437ebbd9fb28bdeb835b57769f')


prepare() {
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name "Warsow" --categories "Game;ActionGame"
}

build() {
  # Compile Warsow
  cd source/source/

  sed -i 's|static short \*snd_out|short \*snd_out|g' snd_qf/snd_mix.c

  make
}

package() {
  cd source/source/

  # Create Destination Directories
  install -d $pkgdir/usr/local/warsow/share/warsow/

  # Move Compiled Data to Destination Directory
  cp -r release/* $pkgdir/usr/local/warsow/share/warsow
  find $pkgdir/usr/local/warsow/share/warsow -type d | xargs chmod 755 
  find $pkgdir/usr/local/warsow/share/warsow -type f | xargs chmod 755 # only executable files in there

  install -D -m 0755 $srcdir/warsow.launcher $pkgdir/usr/local/warsow/bin/warsow
  install -D -m 0755 $srcdir/wsw-server.launcher $pkgdir/usr/local/warsow/bin/wsw-server
  install -D -m 0755 $srcdir/wswtv-server.launcher $pkgdir/usr/local/warsow/bin/wswtv-server
  install -D -m 0644 $srcdir/warsow.desktop $pkgdir/usr/local/warsow/share/applications/warsow.desktop
  #convert win32/warsow.ico $srcdir/warsow.png
  install -D -m 0644 $srcdir/warsow.png $pkgdir/usr/local/warsow/share/pixmaps/warsow.png
}

# vim: ts=2:sw=2
