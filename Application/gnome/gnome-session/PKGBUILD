# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.22.2
pkgrel=2
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
# depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
depends=(systemd dconf gsettings-desktop-schemas gtk3
         json-glib libgl libsm libxtst systemd)
makedepends=(intltool mesa gtk-doc xtrans)
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=(gnome)
_commit=f5d6a6da547e048c0f4a5a13e5113abbed9cbcb3  # tags/3.22.2^0
source=("git://git.gnome.org/gnome-session#commit=$_commit")
sha256sums=('SKIP')


if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Environment fixes for systemd and Wayland
  git cherry-pick -n 52a3c15a1d756c559402cc9505926a9b9d6cf3a7
  git cherry-pick -n 0af8ef83205fcb4ee52a9f66121d20177b050f65
  git cherry-pick -n 7e307f8ddb91db5d4051c4c792519a660ba67f35

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
      --localstatedir=/var --libexecdir=/usr/local/${dstdir}/lib/gnome-session \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
