# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.46.0
pkgrel=2
pkgdesc="Javascript Bindings for GNOME"
arch=(i686 x86_64)
url="http://live.gnome.org/Gjs"
license=(GPL)
# depends=(cairo gobject-introspection-runtime 'js>=24.2.0' 'gtk3')
depends=(cairo 'gtk3')
makedepends=('gobject-introspection')
_commit=caeb97adf1f3de02fe5c9d0cd3c1434999a1a6ce
source=("git+https://gitlab.gnome.org/GNOME/gjs#commit=$_commit")
sha256sums=('SKIP')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
else
  dstdir=gnome3
fi

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir} --disable-static --libexecdir=/usr/local/${dstdir}/lib
  # fix bug in gcc 11
  sed -i 's|static volatile size_t|static size_t|g' libgjs-private/gjs-util.cpp
  make
}

check() {
  cd $pkgname
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
