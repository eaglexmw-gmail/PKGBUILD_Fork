# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Martin HÃ¤ger <martin.haeger@gmail.com>

pkgname=liboauth
pkgver=1.0.3+9+g11e9461
pkgrel=1
pkgdesc='C library implementing OAuth Core RFC 5849'
arch=('i686' 'x86_64')
url="https://github.com/x42/liboauth"
license=('MIT')
depends=('curl')
_commit=11e9461fc433e3787198f9c01a0a565a8a1f90a9
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  autoreconf -fvi
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

build() {
  cd "$srcdir/$pkgname"

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir} --disable-static
  make

}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING.MIT "$pkgdir/usr/local/${dstdir}/share/licenses/$pkgname/COPYING"
}
