#$Id$
#Maintainer: Jan De Groot <jgc@archlinux.org>

pkgname=libgnome-keyring
pkgver=3.12.0
pkgrel=2
pkgdesc="GNOME keyring client library"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('dbus' 'glib2' 'libgcrypt')
makedepends=('intltool' 'gobject-introspection' 'vala' 'python2')
optdepends=('gnome-keyring: key storage service (or use any other service implementing org.freedesktop.secrets)')
options=('!emptydirs')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('c4c178fbb05f72acc484d22ddb0568f7532c409b0a13e06513ff54b91e947783')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

build() {
  cd "$pkgname-$pkgver"

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/local/${dstdir}/lib/gnome-keyring
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
