# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.20.0
pkgrel=2
pkgdesc="GNOME Flashback session"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
license=('GPL')
# depends=('gnome-bluetooth' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'gnome-themes-standard' 'libibus' 'metacity' 'nautilus')
makedepends=('intltool')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management'
            'notification-daemon: Desktop notifications')
install=$pkgname.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        fix-theme.patch)
sha256sums=('0f065d8eb7ad8939734bab551cde39ddf327ada54ff38c32737f2f29dd7111c9'
            '5fc12caeb38f4bbeffd2679397d62aa0e300464cbf88726d4b04d279742fa481')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi


prepare() {
  cd $pkgname-$pkgver

  # Small theme improvements
  # https://bugzilla.gnome.org/show_bug.cgi?id=764857
  patch -Np1 -i ../fix-theme.patch
}

build() {
  cd $pkgname-$pkgver

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc --localstatedir=/var --libexecdir=/usr/local/${dstdir}/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
