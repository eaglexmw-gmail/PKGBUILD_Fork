# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.22.0
pkgrel=2
pkgdesc="GNOME Flashback session"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
license=('GPL')
# depends=('gnome-bluetooth' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'gnome-themes-standard' 'libibus' 'metacity' 'nautilus')
makedepends=('intltool')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management'
            'notification-daemon: Desktop notifications')
install=$pkgname.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        0001-end-session-dialog-ignore-invalid-inhibitor.patch
        0001-menus-Don-t-exclude-any-desktop-files.patch)
sha256sums=('b4a9e891879e99fb1b02854e7d01ff4f89c1917434de4adb5c7430a71b85a0ec'
            '7cab34d50a30387327d6e6ce049692a886f5dbf6b6afa0791467c69c10916e44'
            '0caa399bf9ccf4eb098202dd8e6cefa0b80f5001596d04031f5448b5c35da5de')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi


prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../0001-end-session-dialog-ignore-invalid-inhibitor.patch
  patch -Np1 -i ../0001-menus-Don-t-exclude-any-desktop-files.patch
}

build() {
  cd $pkgname-$pkgver

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc --localstatedir=/var --libexecdir=/usr/local/${dstdir}/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
