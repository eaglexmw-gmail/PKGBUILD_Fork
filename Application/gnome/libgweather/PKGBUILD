# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=3.18.1
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=('i686' 'x86_64')
license=('LGPL')
# depends=('libsoup' 'gtk3' 'geocode-glib')
depends=('libsoup' 'gtk3')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!emptydirs')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('94b2292f8f7616e2aa81b1516befd7b27682b20acecbd5d656b6954990ca7ad0')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

build() {
    cd "$pkgname-$pkgver"

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

    ./configure --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "$pkgname-$pkgver"
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
}
