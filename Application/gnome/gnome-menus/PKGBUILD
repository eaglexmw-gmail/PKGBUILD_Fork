# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.13.3
pkgrel=2
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection' 'python2')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        support-multiple-desktops-in-XDG_CURRENT_DESKTOP.patch)
sha256sums=('7c446eb8ff381df52b8ba04e5886497595e84d1bc46caf7af764d894736c654e'
            'd35cbc10c4b20bb51514d2ca28c1967de099c50c67266f3ffe0b590a2da1ce7b')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../support-multiple-desktops-in-XDG_CURRENT_DESKTOP.patch
}

build() {
  cd "$pkgname-$pkgver"

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure \
      --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
