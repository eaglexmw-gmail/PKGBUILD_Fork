# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gobject-introspection
pkgname=('gobject-introspection' 'gobject-introspection-runtime')
pkgver=1.48.0
pkgrel=1
url="https://live.gnome.org/GObjectIntrospection"
arch=('x86_64' 'i686')
license=('LGPL' 'GPL')
makedepends=('cairo' 'python2' 'python2-mako')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('fa275aaccdbfc91ec0bc9a6fd0562051acdba731e7d584b64a277fec60e75877')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

prepare() {
  cd $pkgbase-$pkgver
  sed -i '1s|#!/usr/bin/env python$|&2|' giscanner/*.py
}

build() {
  cd $pkgbase-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr/local/${dstdir} --disable-static --enable-doctool
  make
}

package_gobject-introspection-runtime() {
  pkgdesc="Introspection system for GObject-based libraries - runtime files"
  depends=('glib2')
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-libLTLIBRARIES install-typelibsDATA
}

package_gobject-introspection() {
  pkgdesc="Introspection system for GObject-based libraries"
  depends=("gobject-introspection-runtime=$pkgver" 'python2-mako' 'python2')
  options=('!emptydirs')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" uninstall-libLTLIBRARIES uninstall-typelibsDATA
}
