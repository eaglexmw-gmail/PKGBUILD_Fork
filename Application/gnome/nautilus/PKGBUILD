# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=nautilus
pkgname=(nautilus libnautilus-extension)
pkgver=3.20.0
pkgrel=1
pkgdesc="GNOME file manager"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils dconf 
         libtracker-sparql nautilus-sendto)
makedepends=(intltool gobject-introspection python packagekit)
url="http://www.gnome.org"
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('7ca7995a4d6a77871503dc092ae816584b8d1891730e1b9eed1a1e4a16194293')

depends=(libexif desktop-file-utils dconf)
makedepends=(intltool gobject-introspection python)

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

build() {
  cd $pkgbase-$pkgver

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir} --sysconfdir=/usr/local/${dstdir}/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/local/${dstdir}/lib/nautilus \
      --disable-update-mimedb \
      --disable-schemas-compile \
      --disable-selinux --enable-xmp=no --enable-tracker=no
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package_nautilus() {
  depends+=(libnautilus-extension)
  groups=(gnome)
  install=nautilus.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

### Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/local/${dstdir}/{lib,share}
  mv "$pkgdir"/usr/local/${dstdir}/include n-e/usr/local/${dstdir}
  mv "$pkgdir"/usr/local/${dstdir}/lib/{girepository-1.0,pkgconfig} n-e/usr/local/${dstdir}/lib
  mv "$pkgdir"/usr/local/${dstdir}/lib/libnautilus-extension.so* n-e/usr/local/${dstdir}/lib
  mv "$pkgdir"/usr/local/${dstdir}/share/{gir-1.0,gtk-doc} n-e/usr/local/${dstdir}/share
}

package_libnautilus-extension() {
  pkgdesc="Library for extending the $pkgdesc"
  depends=(gtk3)

  mv n-e/* "$pkgdir"
}
