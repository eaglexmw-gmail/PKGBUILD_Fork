#$Id:
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=geocode-glib
pkgver=3.20.1+1+ga646012
pkgrel=1
pkgdesc="Geocode Helper library"
arch=(i686 x86_64)
license=('GPL')
url="https://git.gnome.org/browse/geocode-glib/"
depends=('glib2' 'json-glib' 'libsoup')
makedepends=('intltool' 'gobject-introspection')
options=('!emptydirs')
_commit=a64601202c6cbdb2578aa054f6c3a1249cf3596c
source=("git://git.gnome.org/geocode-glib#commit=$_commit")
sha256sums=('SKIP')

if [ "$CARCH" = "x86_64" ]; then
  dstdir=gnome3_64
elif [ "$CARCH" = "i686" ]; then
  dstdir=gnome3
fi

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i 's/gnome/Adwaita/g' icons/Makefile.am
  NOCONFIGURE=1 gnome-autogen.sh
}

build() {
  cd $pkgname

  export PKG_CONFIG_PATH=${PKG_CONFIG_PATH}:/usr/local/${dstdir}/lib/pkgconfig/
  export XDG_DATA_DIRS=$XDG_DATA_DIRS:/usr/local/${dstdir}/share
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/local/${dstdir}/lib

  ./configure --prefix=/usr/local/${dstdir}
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}

